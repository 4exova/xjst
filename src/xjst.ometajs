ometa XJSTParser <: BSJSParser {

    const = [#string :s] | [#number :n],

    isKeyword :x = ?(BSJSParser._isKeyword(x) || x === 'local' || x === 'template'),

    stmt = "local" "(" expr:c ")" stmt:t -> XJSTParser.match([#local, c, t], 'local')
        | ^stmt,

    local = [#local localExprs:es :t] -> {
            var id = XJSTParser._getLocalId(), res = [#begin];
            es.forEach(function(x){ res.push.apply(res, x[0]) });
            res.push(t);
            es.reverse().forEach(function(x){ res.push.apply(res, x[1]) });
            res;
        },

    localExprs = localExpr:e1 -> [e1] |
        [#binop ',' localExprs:es localExpr:e2] -> { es.push(e2); es },

    localExpr = [#set [#get :n]:p :v] -> {
            var id = XJSTParser._getLocalId();
            [
                [
                    [#var, ['__v' + id, p]],
                    [#set, [#get, n], v]
                ],
                [
                    [#set, [#get, n], [#get, '__v' + id]]
                ]
            ]
        } |
        [#set [#getp const:k :o]:p :v] -> {
            var id = XJSTParser._getLocalId(),
                vTree = [#getp, k, [#get, '__o' + id]];
            [
                [
                    [#var,
                        ['__o' + id, o],
                        ['__v' + id, vTree]],
                    [#set, vTree, v]
                ],
                [
                    [#set, vTree, [#get, '__v' + id]]
                ]
            ]
        } |
        [#set [#getp :k :o]:p :v] -> {
            var id = XJSTParser._getLocalId(),
                vTree = [#getp, [#get, '__k' + id], [#get, '__o' + id]];
            [
                [
                    [#var,
                        ['__o' + id, o],
                        ['__k' + id, k],
                        ['__v' + id, vTree]],
                    [#set, vTree, v]
                ],
                [
                    [#set, vTree, [#get, '__v' + id]]
                ]
            ]
        },

    subMatch = [#binop '===' :e1 const:c] -> [e1, c] |
        [#binop '===' const:c :e2] -> [e2, c] |
        :e3 -> [[#unop, '!', e3], [#get, #false]],

    expr2match = [#binop '&&' expr2match:ms subMatch:m1] -> { ms.push(m1); ms} |
        subMatch:m2 -> [m2] ,

    template = "template" "(" expr:m ")" stmt:b -> [XJSTParser.match(m, #expr2match), b],

    topLevel = (template:t -> [#template, t] | srcElem:s -> [#stmt, s])+:ts spaces end -> XJSTParser._splitTemplates(ts)

}

XJSTParser._getLocalIdCounter = 0;
XJSTParser._getLocalId = function() {
    return this._getLocalIdCounter++;
};

XJSTParser._splitTemplates = function(ts) {
    var templates = [],
        other = [],
        i;

    while(i = ts.shift())
        i[0] == 'template'?
            templates.unshift(i[1]) :
            other.push(i[1]);

    return [other, XJSTParser._identify(templates)];
};

XJSTParser._identify = function(templates) {
    var predicates = new Identifier();
    templates.forEach(function(template) {
        template[0].forEach(function(subMatch) {
            subMatch.unshift(predicates.identify(subMatch[0]));
        })
    });
    return templates;
};

ometa XJSTCompiler <: BSJSTranslator {

    subMatch = [:id trans:m [#get #true]] -> m |
        [:id trans:e trans:c] -> (e + ' === ' + c),

    tMatch = [subMatch:m] -> m |
        [subMatch+:ms] -> ms.join(' && '),

    tBody = trans:e -> e,

    template = [tMatch:m tBody:b] -> ('    if(' + m + ') {' + b + ';return}'),

    templates = [template*:ts] -> ('exports.apply = function(c){\n' + ts.join('\n') +'\n};'),

    other = [trans*:o] -> o.join(';'),

    topLevel = [other:o templates:t] -> {
        '(function(exports){' +
        o + ';' + t +
        'return exports})(typeof exports === "undefined"? {} : exports)' }

}
